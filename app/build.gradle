buildscript {

    repositories {
        mavenCentral()

    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootProject.ext.kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$rootProject.ext.kotlin_version"
    }

}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.neenbedankt.android-apt'

/* The following should be moved from build ? maybe later */
def VUFORIA_SDK_DIR = '..'
def NATIVE_LIB_DIR = 'build/lib'
def JAR_DIR = 'build/java/vuforia'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}
// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir "$VUFORIA_SDK_DIR/$NATIVE_LIB_DIR/"
    }
    defaultConfig {
        applicationId 'com.marked.pixsee'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    archivesBaseName = rootProject.projectDir.getName()
    buildTypes {
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles(files { file('./proguard').listFiles() })
            ndk {
                abiFilters "armeabi-v7a"
            }
        }
        debug {
            debuggable true
            shrinkResources true
            useProguard true
//            ndk {
//                abiFilters "armeabi-v7a"
//            }
        }
    }
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {

        }
    }
    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    def dependency = rootProject.ext
    def app = dependency.presentationDependencies
    compile files('C:/Users/Marked/Documents/git/pixsee/libs/jpct_ae.jar')
    compile files("$VUFORIA_SDK_DIR/$JAR_DIR/Vuforia.jar")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile('io.socket:socket.io-client:0.6.3') {
        exclude group: 'org.json', module: 'json'
    }
    compile app.supportAppcompat
    compile app.ankoSQL
    compile app.gcmService
    compile app.appInvitesService
    compile app.visionService
    compile app.supportRecyclerView
    compile app.supportDesign
    compile app.chatMessageView
    compile app.dilatingProgressBar
    compile app.fab
    compile app.sideMenu
    compile app.fresco
    compile app.frescoImgPipeline
    compile app.rxAndroid
    compile app.retrofit
    compile app.retrofitConverterGson
    compile app.retrofitRX
    compile app.okHttpLogger
    compile app.subSamplingScaleImage
    compile app.dagger

    apt app.daggerCompiler

    provided app.daggerCompiler
    provided app.glassFishAnnotations

    testCompile "junit:junit:$dependency.junitVersion"
    testCompile "org.mockito:mockito-all:$dependency.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$dependency.hamcrestVersion"
    testCompile "org.powermock:powermock-module-junit4:$dependency.powerMockito"
    testCompile "org.powermock:powermock-api-mockito:$dependency.powerMockito"

    androidTestCompile "com.android.support:support-annotations:$dependency.supportLibraryVersion"
    androidTestCompile "com.android.support.test:runner:$dependency.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$dependency.runnerVersion"

// Espresso UI Testing
    // Espresso UI Testing dependencies.
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$dependency.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$dependency.espressoVersion"
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$dependency.espressoVersion") {
        exclude module: 'recyclerview-v7'
    }
}


apply plugin: 'com.google.gms.google-services'

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}